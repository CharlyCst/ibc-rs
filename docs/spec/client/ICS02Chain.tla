--------------------------- MODULE ICS02Chain ---------------------------

EXTENDS Integers, FiniteSets, ClientHandlers, ICS02Definitions
        
CONSTANTS MaxHeight, \* maximal chain height
          ChainID \* chain identifier  

VARIABLES chainStore, \* chain store, containing client heights, a connection end, a channel end 
          incomingDatagrams, \* set of incoming datagrams
          clientCreatedHistory, \* history variable
          clientUpdatedHistory \* history variable

vars == <<chainStore, incomingDatagrams, clientCreatedHistory, clientUpdatedHistory>>
Heights == 1..MaxHeight \* set of possible heights of the chains in the system          

(***************************************************************************
 Client update operators
 ***************************************************************************)
\* Update the clients on chain with chainID, 
\* using the client datagrams generated by the relayer      
\* (Handler operators defined in ClientHandlers.tla)
LightClientUpdate(chainID, store, datagrams) == 
    LET clientID1 == GetCounterpartyClientID1(chainID) IN
    LET clientID2 == GetCounterpartyClientID2(chainID) IN
    \* create clients
    LET clientCreatedStore1 == AsChainStore(HandleCreateClient(chainID, store, clientID1, datagrams)) IN
    LET clientCreatedStore2 == AsChainStore(HandleCreateClient(chainID, clientCreatedStore1, clientID2, datagrams)) IN
    \* update clients
    LET clientUpdatedStore1 == AsChainStore(HandleClientUpdate(chainID, clientCreatedStore2, clientID1, datagrams)) IN
    LET clientUpdatedStore2 == AsChainStore(HandleClientUpdate(chainID, clientUpdatedStore1, clientID2, datagrams)) IN

    clientUpdatedStore2

(***************************************************************************
 Chain actions
 ***************************************************************************)       
\* Advance the height of the chain until MaxHeight is reached
AdvanceChain ==
    /\ chainStore.height + 1 \in Heights
    /\ chainStore' = [chainStore EXCEPT !.height = chainStore.height + 1]
    /\ UNCHANGED <<incomingDatagrams, clientCreatedHistory, clientUpdatedHistory>>

\* Handle the datagrams and update the chain state        
HandleIncomingDatagrams ==
    /\ incomingDatagrams /= AsSetDatagrams({})
    /\ chainStore' = LightClientUpdate(ChainID, chainStore, incomingDatagrams) 
    \* for each clID, record when the client is created (either in client1 or client2) 
    /\ clientCreatedHistory' = 
        [clID \in GetCounterpartyClientIDs(ChainID) |-> 
            IF /\ ~clientCreatedHistory[clID] 
               /\ \/ /\ chainStore'.client1.clientID = clID
                     /\ chainStore.client1.clientID = nullClientID
                  \/ /\ chainStore'.client2.clientID = clID
                     /\ chainStore.client2.clientID = nullClientID
            THEN TRUE 
            ELSE clientCreatedHistory[clID]
        ]
    \* record if in this transition the client with clID is updated     
    /\ clientUpdatedHistory' = 
        [clID \in GetCounterpartyClientIDs(ChainID) |-> 
            IF \/ /\ chainStore'.client1.clientID = clID
                  /\ chainStore.client1.heights /= AsSetInt({})
                  /\ chainStore.client1.heights \subseteq chainStore'.client1.heights  
               \/ /\ chainStore'.client2.clientID = clID
                  /\ chainStore.client2.heights /= AsSetInt({})
                  /\ chainStore.client2.heights \subseteq chainStore'.client2.heights
            THEN TRUE 
            ELSE FALSE
        ]    
    /\ incomingDatagrams' = AsSetDatagrams({})
       

(***************************************************************************
 Specification
 ***************************************************************************)
\* Initial state predicate
\* Initially
\*  - each chain is initialized to InitChain (defined in RelayerDefinitions.tla)
\*  - pendingDatagrams for each chain is empty
\*  - the packetSeq is set to 1
Init == 
    /\ chainStore = ICS02InitChainStore
    /\ incomingDatagrams = AsSetDatagrams({})
    /\ clientCreatedHistory = [clID \in GetCounterpartyClientIDs(ChainID) |-> FALSE]
    /\ clientUpdatedHistory = [clID \in GetCounterpartyClientIDs(ChainID) |-> FALSE]
    
\* Next state action
\* The chain either
\*  - advances its height
\*  - receives datagrams and updates its state
\*  - sends a packet if the appPacketSeq is not bigger than MaxPacketSeq
\*  - acknowledges a packet
Next ==
    \/ AdvanceChain 
    \/ HandleIncomingDatagrams
    \/ UNCHANGED vars
        
Fairness ==
    /\ WF_vars(AdvanceChain)
    /\ WF_vars(HandleIncomingDatagrams)        
        
(***************************************************************************
 Invariants
 ***************************************************************************)
\* Type invariant   
\* ChainStores and Datagrams are defined in RelayerDefinitions.tla        
TypeOK ==    
    /\ chainStore \in ChainStores(MaxHeight)
    /\ incomingDatagrams \in SUBSET Datagrams(MaxHeight)
    
\* two clients with the same ID cannot be created    
CreatedClientsHaveDifferentIDs ==
    (/\ chainStore.client1.clientID /= nullClientID
     /\ chainStore.client2.clientID /= nullClientID)
        => chainStore.client1.clientID /= chainStore.client2.clientID
        
\* only created clients can be updated
UpdatedClientsAreCreated ==
    \A clID \in GetCounterpartyClientIDs(ChainID) : 
        clientUpdatedHistory[clID] => clientCreatedHistory[clID]

(***************************************************************************
 Properties
 ***************************************************************************)    
\* it ALWAYS holds that the height of the chain does not EVENTUALLY decrease
HeightDoesntDecrease ==
    [](\A h \in Heights : chainStore.height = h 
        => <>(chainStore.height >= h))
        
=============================================================================
\* Modification History
\* Last modified Tue Oct 13 14:09:47 CEST 2020 by ilinastoilkovska
\* Created Fri Jun 05 16:56:21 CET 2020 by ilinastoilkovska
